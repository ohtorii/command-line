/*
＊コマンド例
	!dir /b
	varren3.mac
	message("Hello world.")
*/

#g_open_newfile = false;
#g_new_hidemaru = 0;
#g_old_hidemaru = hidemaruhandle(0);

disablebreak;
call Main;
endmacro;



Main:
	call NewfileIfExist;
	if(##return==false){
		return false;
	}
	call LoadDengakuDll;
	if(! ##return){
		return ;
	}
	call MainLoop;
	clearupdated;
	//if(! ##result)
	{
		call CloseFileIfOpen;
	}
	return;


MainLoop:
	$$key_buffer="";
	while(true){
		execmacro currentmacrodirectory+"\\command-line-internal\\input-keys.mac", $$key_buffer;
		$$keybord_buffer=getresultex(-1);
		$$status=leftstr($$keybord_buffer,1);
		$$key_buffer=midstr($$keybord_buffer,1);
		
		if($$status=="0"){
			/*pass*/
		}else if($$status=="1"){
			/*Enterキーが押された*/
			ProcessEnter $$key_buffer;
		}else if($$status=="2"){
			//Escapeが押された
			return ;
		}else{
			/*pass*/
		}
	}
	return ;

ProcessEnter:
	$$key_buffer=$$1;
	if($$key_buffer==""){
		return;
	}
	if(leftstr($$key_buffer,1)=="!"){
		//シェル実行
		call RunShell $$key_buffer;
	}else{
		//秀丸マクロ or 秀丸マクロの関数
		call RunHidemaruCommand $$key_buffer;
	}
	return;

RunShell:
	//!を取り除く
	$$command=midstr($$1,1);
	runex "cmd.exe /c "+$$command
			, 0 			//sync	  0:async 1:sync
			, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
			, 5, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
			, 5, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
			, 1, "" 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
			, 2 			//show	  0:auto 1:show 2:hide
			, 0 			//nodraw  0:draw 1:no draw
			, 0 			//unicode 0:ansi 2:unicode
			;
	return ;

RunHidemaruCommand:
	//todo: 空白区切りは文字列を解析できないのでよくない。
	//		（例）hoge.mac 1 2 "a b c";
	call Tokenize, $$1, " ";
	//call DumpToken;
	if(#g_token_num==0){
		return ;
	}
	$$command = $g_token[0];
	//マクロディレクトリからファイルを探す
	$$abs_filename=macrodir+"\\"+$$command;
	if(existfile($$abs_filename)){
		call ExecMacro $$abs_filename;
		return ;
	}
	
	//マクロファイルが無ければ秀丸のコマンドとして実行する
	eval($$1);
	return ;
	
ExecMacro:
	$$abs_filename=$$1;
	if(#g_token_num==1){
		execmacro $$abs_filename;
	}else if(#g_token_num==2){
		execmacro $$abs_filename, $g_token[1];
	}else if(#g_token_num==3){
		execmacro $$abs_filename, $g_token[1], $g_token[2];
	}else if(#g_token_num==4){
		execmacro $$abs_filename, $g_token[1], $g_token[2], $g_token[3];
	}else if(#g_token_num==5){
		execmacro $$abs_filename, $g_token[1], $g_token[2], $g_token[3], $g_token[4];
	}else if(#g_token_num==6){
		execmacro $$abs_filename, $g_token[1], $g_token[2], $g_token[3], $g_token[4], $g_token[5];
	}
	return ;
/*
＊引数
	$$1 対象文字列
	$$2 セパレータ

＊返値
	#g_token_num	トークン数
	$g_token[]		トークンの配列
*/
Tokenize:
	#g_token_num=0;
	$$token = dllfuncstr("GETTOKEN",$$1,$$2);
	while (true) {
		if($$token==""){
			return;
		}
		$g_token[#g_token_num] = $$token;
		#g_token_num = #g_token_num + 1;
	    if (dllfunc("HASMORETOKENS") == 0) {
			return;
		}
	    $$token = dllfuncstr("GETTOKEN","",$$2);
	}
	return;


DumpToken:
	debuginfo "==== DumpToken ====";
	##i=0;
	while(##i<#g_token_num){
		debuginfo "["+str(##i)+"]"+$g_token[##i];
		##i = ##i + 1;
	}
	return ;


NewfileIfExist:
	#g_open_newfile = false;
	if(filetype=="new"){
		return true;
	}
	//編集中のテキストを上書きしないための対応。
	//新規ファイルを作成しそこへコマンドの実行結果を出力する。
	newfile;
	if(result==0){
		return false;
	}
	#g_open_newfile=true;
	#g_new_hidemaru=hidemaruhandle(0);
	return true;


CloseFileIfOpen:
	if(#g_open_newfile){
		setactivehidemaru	#g_old_hidemaru;
		closehidemaruforced #g_new_hidemaru;

		#g_old_hidemaru=0;
		#g_new_hidemaru=0;
	}
	return true;

LoadDengakuDll:
	loaddll macrodir + "\\DengakuDLL.dll";
	if (result) {
		return true;
	}
	loaddll hidemarudir + "\\DengakuDLL.dll";
	if (result) {
		return true;
	}
	message "田楽DLLのロードに失敗しました\n" + "DengakuDLL.dllが秀丸マクロディレクトリに存在するか確認してください";
	return false;
